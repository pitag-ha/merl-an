open! Import

module Cache_workflow = struct
  type t =
    | Buffer_typed
    (* | File_hash_diff  *)
    (* | Cmis_cached  *)
    | No_cache
  [@@deriving yojson_of, enumerate]

  let to_string = function
    | Buffer_typed -> "buffer-typed"
    (* | File_hash_diff -> "file-hash-diff"  *)
    (* | Cmis_cached -> "cmis-cached" *)
    | No_cache -> "no-cache"

  let description = function
    | Buffer_typed -> "Buffer is typed"
    (* | File_hash_diff -> "Buffer is typed; file contents changed but AST did not" *)
    (* | Cmis_cached -> "Buffer is not typed; cmis are cached" *)
    | No_cache -> "Buffer is not typed; cmis are not cached"

  let uses_server cache =
    match cache with
    (* | Cmis_cached  *)
    (* | File_hash_diff  *)
    | Buffer_typed -> true
    | No_cache -> false

  let print path = function
    (* | Cmis_cached  *)
    (* | File_hash_diff *)
    | Buffer_typed -> Format.sprintf "%s server" (Fpath.to_string path)
    | No_cache -> Format.sprintf "%s single" (Fpath.to_string path)
end

module Path = struct
  type t = Fpath.t

  let yojson_of_t path = `String (Fpath.to_string path)
end

type t = {
  id : int;
  path : Path.t;
  cache_workflow : Cache_workflow.t;
  version : Yojson.Safe.t;
  comment : string option;
}
[@@deriving yojson_of]

let pp ppf merlin =
  Format.fprintf ppf "%s%!" (Yojson.Safe.to_string (yojson_of_t merlin))

let get_id m = m.id
let is_server merlin = Cache_workflow.uses_server merlin.cache_workflow

let basic_cmd ppf { path; cache_workflow; _ } =
  Format.fprintf ppf "%s" (Cache_workflow.print path cache_workflow)

let untimed_query_generic ~f cmd =
  let ic = Unix.open_process_in cmd in
  match f ic with
  | res -> (
      match Unix.close_process_in ic with
      | Unix.WEXITED 0 -> res
      | Unix.WEXITED code ->
          Format.eprintf "merlin exited with code %s\n%!" (string_of_int code);
          exit 1
      | _ ->
          Format.eprintf "merlin closed unexpectedly\n%!";
          exit 2)
  | exception e ->
      ignore (Unix.close_process_in ic);
      let s =
        Format.sprintf "exception while running [%s]: %s\n%!" cmd
          (Printexc.to_string e)
      in
      failwith s

let untimed_query_json cmd =
  let f = Yojson.Safe.from_channel in
  untimed_query_generic ~f cmd

let untimed_query_str cmd =
  let f = input_line in
  untimed_query_generic ~f cmd

let make id ?comment path cache_workflow =
  let cmd =
    Printf.sprintf "%s -version" (Cache_workflow.print path cache_workflow)
  in
  let version = `String (untimed_query_str cmd) in
  { id; path; cache_workflow; version; comment }

module Query_type = struct
  type t =
    | Case_analysis
    | Type_enclosing
    | Occurrences
    | Complete_prefix
    | Expand_prefix
    | Locate
    | Errors
  [@@deriving yojson_of, enumerate]

  let to_string = function
    | Locate -> "locate"
    | Case_analysis -> "case-analysis"
    | Type_enclosing -> "type-enclosing"
    | Occurrences -> "occurrences"
    | Complete_prefix -> "complete-prefix"
    | Expand_prefix -> "expand-prefix"
    | Errors -> "errors"

  type node =
    | Longident
    | Expression
    | Var_pattern
    | Module_expr
    | Module_decl
    | Module_type_decl
  [@@deriving yojson]

  let is_global = function
    | Locate | Case_analysis | Type_enclosing | Occurrences | Complete_prefix
    | Expand_prefix ->
        false
    | Errors -> true

  let has_target qt target =
    let target_nodes =
      match qt with
      | Locate -> [ Longident ]
      | Case_analysis -> [ Expression; Var_pattern ]
      | Type_enclosing ->
          [ Expression; Module_expr; Module_decl; Module_type_decl ]
      | Occurrences -> [ Longident ]
      | Complete_prefix -> [ Longident ]
      | Expand_prefix -> [ Longident ]
      | Errors ->
          (* ToDo: this could by type-safer!! *)
          []
    in
    List.mem target target_nodes
end

module Response = struct
  type t = Yojson.Safe.t

  let yojson_of_t x = x

  let get_timing = function
    | `Assoc answer -> (
        match List.assoc "timing" answer with
        | `Assoc timing -> (
            match List.assoc "clock" timing with
            | `Int time -> time
            | _ -> failwith "merlin gave bad output")
        | _ -> failwith "merlin gave bad output")
    | _ -> failwith "merlin gave bad output"

  let crop_timing = function
    | `Assoc answer -> `Assoc (List.remove_assoc "timing" answer)
    | _ ->
        (* Fixme *)
        failwith
          "Error while cropping merlin response: reponse should have a key \
           called timing."

  let crop_value = function
    | `Assoc answer -> `Assoc (List.remove_assoc "value" answer)
    | _ ->
        (* Fixme *)
        failwith
          "Error while cropping merlin response: reponse should have a key \
           called value."

  let is_successful = function
    | `Assoc answer -> (
        match List.assoc "timing" answer with
        | `Assoc timing -> (
            match List.assoc "clock" timing with
            | `Int _time -> true
            | _ -> false)
        | _ -> false)
    | _ -> false
end

module Cmd = struct
  type t = string

  let yojson_of_t cmd = `String cmd

  (* FIXME: instead of having loc optional, it would be nice to have a logic containing: if query_type is local, there's a loc; if it's global, there's none *)
  let make ~query_type ~file ?li ?loc merlin =
    let retrieve_loc ~query_type = function
      | Some loc -> Ok loc
      | None ->
          Error
            (Logs.Error
               (Format.sprintf "Didn't receive a location for %s"
                  (Query_type.to_string query_type)))
    in
    let open Result.Syntax in
    let* query_cmd =
      match query_type with
      | Query_type.Locate ->
          let* loc = retrieve_loc ~query_type loc in
          Result.ok
          @@ Format.asprintf
               " %a %s -look-for ml -position '%a' -index 0 -filename %a < %a"
               basic_cmd merlin
               (Query_type.to_string query_type)
               (Location.print_edge Right)
               loc File.pp file File.pp file
      | Case_analysis ->
          let* loc = retrieve_loc ~query_type loc in
          Result.ok
          @@ Format.asprintf "%a %s -start '%a' -end '%a' -filename %a < %a"
               basic_cmd merlin
               (Query_type.to_string query_type)
               (Location.print_edge Left) loc
               (Location.print_edge Right)
               loc File.pp file File.pp file
      | Type_enclosing ->
          let* loc = retrieve_loc ~query_type loc in
          Result.ok
          @@ Format.asprintf "%a %s -position '%a' -index 0 -filename %a < %a"
               basic_cmd merlin
               (Query_type.to_string query_type)
               (Location.print_edge Right)
               loc File.pp file File.pp file
      | Occurrences ->
          let* loc = retrieve_loc ~query_type loc in
          Result.ok
          @@ Format.asprintf "%a %s -identifier-at '%a' -filename %a < %a"
               basic_cmd merlin
               (Query_type.to_string query_type)
               (Location.print_edge Right)
               loc File.pp file File.pp file
      | Complete_prefix | Expand_prefix -> (
          (* TODO: for expand-prefix, it might be interesting to modify the source code to introduce an error *)
          match li with
          | Some li ->
              let* loc = retrieve_loc ~query_type loc in
              let first_half s = String.(sub s 0 ((length s / 2) + 1)) in
              Result.ok
              @@ Format.asprintf
                   "%a %s -prefix %s -position '%a' -filename %a < %a" basic_cmd
                   merlin
                   (Query_type.to_string query_type)
                   (first_half @@ Longident.name li)
                   (Location.print_edge Right)
                   loc File.pp file File.pp file
          | None ->
              Error
                (Logs.Error
                   (Format.sprintf
                      "The sampling workflow didn't collect info about the \
                       longident for %s"
                      (Query_type.to_string query_type))))
      | Errors ->
          Result.ok
          @@ Format.asprintf "%a %s -filename %a < %a" basic_cmd merlin
               (Query_type.to_string query_type)
               File.pp file File.pp file
    in
    let cmd =
      match merlin.cache_workflow with
      (* | Cache.File_hash_diff -> *)
      (* TODO: clean up afterwards! *)
      (* Format.asprintf "echo '' >> %a && %s" File.pp file query_cmd *)
      (* | Cmis_cached ->  *)
      (* Format.asprintf "sed -i '1s/^/let () = ()\n/' %a " File.pp file *)
      (* Format.asprintf "echo -e 'let () = ()\n$(cat %a)' > %a " File.pp file File.pp file *)

      (* query_cmd FIXME!!! *)
      | Buffer_typed | No_cache -> query_cmd
    in
    Result.ok cmd

  let some_global_cmd file merlin =
    Format.asprintf "%a errors -filename %a < %a" basic_cmd merlin File.pp file
      File.pp file

  let run_once cmd =
    (* FIXME: it would be better to catch the exception here. *)
    let repl = untimed_query_json cmd in
    repl

  let run ~repeats cmd =
    try
      let rec loop responses = function
        | 0 -> responses
        | n ->
            let new_resp = run_once cmd in
            loop (new_resp :: responses) (n - 1)
      in
      Ok (loop [] repeats)
    with exc -> Error (Logs.Error (Printexc.to_string exc))
end

let init_cache file merlin =
  let cmd = Cmd.some_global_cmd file merlin in
  try
    let _ = Cmd.run_once cmd in
    Ok ()
  with exc -> Error (Logs.Error (Printexc.to_string exc))

let stop_server { path; _ } =
  let command = Fpath.to_string path ^ " server stop-server" in
  match Sys.command command with
  | 255 -> ()
  | code -> failwith ("merlin exited with code " ^ string_of_int code)
